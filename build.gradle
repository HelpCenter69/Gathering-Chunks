subprojects {

    apply plugin: 'java'

    group = 'com.ryvione'

    java.toolchain.languageVersion = JavaLanguageVersion.of(21)
    java.withSourcesJar()
    java.withJavadocJar()

    sourcesJar {
        from(rootProject.file("LICENSE")) {
            rename { "${it}_${mod_name}" }
        }
    }

    repositories {

        mavenCentral()

        maven {
            name = 'Sponge / Mixin'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }

        maven {
            name = 'BlameJared Maven (CrT / Bookshelf)'
            url = 'https://maven.blamejared.com'
        }

        maven {
            name = "ModMaven"
            url = "https://modmaven.dev"
        }
    }

    tasks.withType(JavaCompile).configureEach {

        it.options.encoding = 'UTF-8'
        it.options.release = 21
    }

    tasks.withType(GenerateModuleMetadata) {

        enabled = false
    }
}

task copyToReleases(type: Copy) {
    dependsOn ':Fabric:build'
    from file("Fabric/build/libs")
    include "*.jar"
    exclude "*-sources.jar"
    exclude "*-javadoc.jar"
    into file("$rootDir/Releases")
    doLast {
        println "Copied jar to Releases/"
    }
}